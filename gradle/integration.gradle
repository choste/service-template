/*************************************************************************************************
 *                                    Integration test configurations                            *
 *This gradle file sets up a new source set directory under src/integration/java which allows    *
 *for integration test to be stored in a separate directory than unit tests. This also creates   *
 *the integrationTest task and puts it into the build chain after test, but before check tasks.  *
 *                                                                                               *
 *Rest assured is used for integration test to provide some nice syntactic sugar around the http *
 *client. Junit is used as a test runner.                                                        *
 *************************************************************************************************/

dependencies {
    //integration dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'com.jayway.restassured:rest-assured:2.7.0'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration/java')
        }
        resources.srcDir file('src/integration/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    outputs.upToDateWhen { false }
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}